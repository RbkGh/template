apply plugin: 'war'
apply plugin: 'ssh'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.hidetake:gradle-ssh-plugin:0.3.6'
    }
}

remotes {
    localhost {
        host = 'localhost'
        user = System.properties['user.name']
        identity = file("${System.properties['user.home']}/.ssh/id_rsa")
    }
}

task showPlatformVersion(type: SshTask) {
    session(remotes.localhost) {
        execute('uname -a')
        execute('cat /etc/*-release || true')
    }
}

task deploy(type: SshTask, dependsOn: war) {
    ext {
        webappsDir = '/webapps'
    }
    session(remotes.localhost) {
        put(war.archivePath.path, webappsDir)
        execute('sudo service tomcat restart')
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.10'
}
